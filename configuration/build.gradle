bootJar.enabled = true
jar.enabled = false

dependencies {
    implementation project(':web-adapter')

    implementation project(':api-in')
    implementation project(':application')
    implementation project(':api-out')

    implementation project(':board-persistence-adapter')
    implementation project(':file-persistence-adapter')

    implementation project(':board-jpa')
    implementation project(':file-jpa')

    implementation project(':support')

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jta-atomikos'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
        resources.srcDir file('src/webapp')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    afterTest { desc, result ->
        logger.quiet "Executing integration test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

tasks.named("bootJar") {
    mainClass = 'me.gogosing.ApplicationInitializr'
}

tasks.named("bootRun") {
    systemProperties = System.properties
}